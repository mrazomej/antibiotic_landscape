{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "(c) 2024 Manuel Razo. This work is licensed under a [Creative Commons\n",
    "Attribution License CC-BY 4.0](https://creativecommons.org/licenses/by/4.0/).\n",
    "All code contained herein is licensed under an [MIT\n",
    "license](https://opensource.org/licenses/MIT)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Import project package\n",
    "import Antibiotic\n",
    "\n",
    "# Import package to handle DataFrames\n",
    "import DataFrames as DF\n",
    "import CSV\n",
    "\n",
    "# Import library for Bayesian inference\n",
    "import Turing\n",
    "import FillArrays\n",
    "\n",
    "# Import library to list files\n",
    "import Glob\n",
    "\n",
    "# Import packages to work with data\n",
    "import DataFrames as DF\n",
    "\n",
    "# Load CairoMakie for plotting\n",
    "using CairoMakie\n",
    "import PairPlots\n",
    "import ColorSchemes\n",
    "\n",
    "# Import basic math libraries\n",
    "import LsqFit\n",
    "import StatsBase\n",
    "import LinearAlgebra\n",
    "import Random\n",
    "\n",
    "# Activate backend\n",
    "CairoMakie.activate!()\n",
    "\n",
    "# Set PBoC Plotting style\n",
    "Antibiotic.viz.theme_makie!()"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Bayesian Inference of $IC_{50}$ values"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "In this notebook, we will perform Bayesian inference on the $IC_{50}$ values of\n",
    "the antibiotic resistance landscape. For this, we will use the raw `OD620`\n",
    "measurements provided by Iwasawa et al. (2022).\n",
    "\n",
    "Let's begin by loading the data into a DataFrame."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Load data into a DataFrame\n",
    "df = CSV.read(\n",
    "    \"$(git_root())/data/Iwasawa_2022/iwasawa_tidy.csv\", DF.DataFrame\n",
    ")\n",
    "\n",
    "first(df, 5)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "To double-check that the structure of the table makes sense, let's plot the time\n",
    "series for one example to see if the sequence agrees with the expectation."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Define data to use\n",
    "data = df[\n",
    "    (df.antibiotic.==\"KM\").&(df.env.==\"Parent_in_KM\").&(df.strain_num.==13).&.!(df.blank).&(df.concentration_ugmL.>0),\n",
    "    :]\n",
    "# Remove blank measurement\n",
    "# Group data by day\n",
    "df_group = DF.groupby(data, :day)\n",
    "\n",
    "# Initialize figure\n",
    "fig = Figure(size=(500, 300))\n",
    "\n",
    "# Add axis\n",
    "ax = Axis(\n",
    "    fig[1, 1],\n",
    "    xlabel=\"antibiotic concentration\",\n",
    "    ylabel=\"OD₆₂₀\",\n",
    "    xscale=log2\n",
    ")\n",
    "\n",
    "# Define colors for plot\n",
    "colors = get(ColorSchemes.Blues_9, LinRange(0.25, 1, length(df_group)))\n",
    "\n",
    "# Loop through days\n",
    "for (i, d) in enumerate(df_group)\n",
    "    # Sort data by concentration\n",
    "    DF.sort!(d, :concentration_ugmL)\n",
    "    # Plot scatter line\n",
    "    scatterlines!(\n",
    "        ax, d.concentration_ugmL, d.OD, color=colors[i], label=\"$(first(d.day))\"\n",
    "    )\n",
    "end # for\n",
    "\n",
    "# Add legend to plot\n",
    "fig[1, 2] = Legend(\n",
    "    fig, ax, \"day\", framevisible=false, nbanks=3, labelsize=10\n",
    ")\n",
    "\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The functional form used by the authors to fit the data is\n",
    "$$\n",
    "f(x) = \\frac{a}\n",
    "{1+\\exp \\left[b\\left(\\log _2 x-\\log _2 \\mathrm{IC}_{50}\\right)\\right]} + c\n",
    "\\tag{1}\n",
    "$$\n",
    "where $a$, $b$, and $c$ are nuisance parameters of the model, $\\mathrm{IC}_{50}$\n",
    "is the parameter of interest, and $x$ is the antibiotic concentration. We can\n",
    "define a function to compute this model."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@doc raw\"\"\"\n",
    "    logistic(logx, a, b, c, logic50)\n",
    "\n",
    "Compute the logistic function used to model the relationship between antibiotic\n",
    "concentration and bacterial growth, using log2 inputs for concentration and\n",
    "IC₅₀.\n",
    "\n",
    "This function implements the following equation:\n",
    "\n",
    "f(x) = a / (1 + exp(b * (log(x) - log(IC₅₀)))) + c\n",
    "\n",
    "# Arguments\n",
    "- `logx`: log of the antibiotic concentration (input variable)\n",
    "- `a`: Maximum effect parameter (difference between upper and lower asymptotes)\n",
    "- `b`: Slope parameter (steepness of the curve)\n",
    "- `c`: Minimum effect parameter (lower asymptote)\n",
    "- `logic50`: log of the IC₅₀ parameter\n",
    "\n",
    "# Returns\n",
    "The computed effect (e.g., optical density) for the given log₂ antibiotic\n",
    "concentration and parameters.\n",
    "\n",
    "Note: This function is vectorized and can handle array inputs for `log2x`.\n",
    "\"\"\"\n",
    "function logistic(logx, a, b, c, logic50)\n",
    "    return @. a / (1.0 + exp(b * (logx - logic50))) + c\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Bayesian model"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Given the model presented in Eq. (1), and the data, our objective is to infer\n",
    "the value of all parameters. By Bayes theorem, we write\n",
    "$$\n",
    "\\pi(\\mathrm{IC}_{50}, a, b, c \\mid \\text{data}) = \n",
    "\\frac{\\pi(\\text{data} \\mid \\mathrm{IC}_{50}, a, b, c) \n",
    "\\pi(\\mathrm{IC}_{50}, a, b, c)}\n",
    "{\\pi(\\text{data})},\n",
    "\\tag{2}\n",
    "$$\n",
    "where $\\text{data}$ consists of the pairs of antibiotic concentration and\n",
    "optical density."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "### Likelihood $\\pi(\\text{data} \\mid \\mathrm{IC}_{50}, a, b, c)$\n",
    "\n",
    "Let's begin by defining the likelihood function. For simplicity, we assume each\n",
    "datum is independent and identically distributed (i.i.d.) and write\n",
    "$$\n",
    "\\pi(\\text{data} \\mid \\mathrm{IC}_{50}, a, b, c) = \n",
    "\\prod_{i=1}^n \\pi(d_i \\mid \\mathrm{IC}_{50}, a, b, c),\n",
    "\\tag{3}\n",
    "$$\n",
    "where $d_i = (x_i, y_i)$ is the $i$-th pair of antibiotic concentration and\n",
    "optical density, respectively, and $n$ is the total number of data points.\n",
    "\n",
    "As a first pass, we assume that our experimental measurements can be expressed\n",
    "as\n",
    "$$\n",
    "y_i = f(x_i, \\mathrm{IC}_{50}, a, b, c) + \\epsilon_i,\n",
    "\\tag{4}\n",
    "$$\n",
    "where $\\epsilon_i$ is the experimental error. Furthermore, we assume that the\n",
    "experimental error is normally distributed, i.e.,\n",
    "$$\n",
    "\\epsilon_i \\sim \\mathcal{N}(0, \\sigma^2),\n",
    "\\tag{5}\n",
    "$$\n",
    "where $\\sigma^2$ is an unknown variance parameter that must be included in our\n",
    "inference. Notice that we assume the same variance parameter for all data\n",
    "points since $\\sigma^2$ is not indexed by $i$.\n",
    "\n",
    "Given this likelihood function, we must update our inference on the parameters\n",
    "as\n",
    "$$\n",
    "\\pi(\\mathrm{IC}_{50}, a, b, c, \\sigma^2 \\mid \\text{data}) = \n",
    "\\frac{\\pi(\\text{data} \\mid \\mathrm{IC}_{50}, a, b, c, \\sigma^2) \n",
    "\\pi(\\mathrm{IC}_{50}, a, b, c, \\sigma^2)}\n",
    "{\\pi(\\text{data})},\n",
    "\\tag{6}\n",
    "$$\n",
    "to include the new parameter $\\sigma^2$.\n",
    "\n",
    "Our likelihood function is then of the form\n",
    "$$\n",
    "y_i \\mid \\mathrm{IC}_{50}, a, b, c, \\sigma^2 \\sim \n",
    "\\mathcal{N}(f(x_i, \\mathrm{IC}_{50}, a, b, c), \\sigma^2).\n",
    "\\tag{7}\n",
    "$$\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Prior $\\pi(\\mathrm{IC}_{50}, a, b, c, \\sigma^2)$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "For the prior, we assume that all parameters are independent and write\n",
    "$$\n",
    "\\pi(\\mathrm{IC}_{50}, a, b, c, \\sigma^2) = \n",
    "\\pi(\\mathrm{IC}_{50}) \\pi(a) \\pi(b) \\pi(c) \\pi(\\sigma^2).\n",
    "\\tag{8}\n",
    "$$\n",
    "\n",
    "Let's detail each prior.\n",
    "\n",
    "1. $\\mathrm{IC}_{50}$: The $IC_{50}$ is a strictly positive parameter. However,\n",
    "we will fit for $\\log_2(\\mathrm{IC}_{50})$. Thus, we will use a normal prior\n",
    "for $\\log_2(\\mathrm{IC}_{50})$. This means we have\n",
    "$$\n",
    "\\log_2(\\mathrm{IC}_{50}) \\sim \n",
    "\\mathcal{N}(\n",
    "    \\mu_{\\log_2(\\mathrm{IC}_{50})}, \\sigma_{\\log_2(\\mathrm{IC}_{50})}^2\n",
    ").\n",
    "\\tag{9}\n",
    "$$\n",
    "\n",
    "2. $a$: This nuisance parameter scales the logistic function. Again, the natural\n",
    "scale for this parameter is a strictly positive real number. Thus, we will\n",
    "use a lognormal prior for $a$. This means we have\n",
    "$$\n",
    "a \\sim \\text{LogNormal}(\\mu_a, \\sigma_a^2).\n",
    "\\tag{10}\n",
    "$$\n",
    "\n",
    "3. $b$: This parameter controls the steepness of the logistic function. Again,\n",
    "the natural scale for this parameter is a strictly positive real number. Thus,\n",
    "we will use a lognormal prior for $b$. This means we have\n",
    "$$\n",
    "b \\sim \\text{LogNormal}(\\mu_b, \\sigma_b^2).\n",
    "\\tag{11}\n",
    "$$\n",
    "\n",
    "4. $c$: This parameter controls the minimum value of the logistic function. \n",
    "Since this is a strictly positive real number that does not necessarily scale\n",
    "with the data, we will use a half-normal prior for $c$. This means we have\n",
    "$$\n",
    "c \\sim \\text{Half-}\\mathcal{N}(0, \\sigma_c^2).\n",
    "\\tag{12}\n",
    "$$\n",
    "\n",
    "5. $\\sigma^2$: This parameter controls the variance of the experimental error.\n",
    "Since this is a strictly positive real number that does not necessarily scale\n",
    "with the data, we will use a half-normal prior for $\\sigma^2$. This means we\n",
    "have\n",
    "$$\n",
    "\\sigma^2 \\sim \\text{Half-}\\mathcal{N}(0, \\sigma_{\\sigma^2}^2).\n",
    "\\tag{13}\n",
    "$$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "With all of this in place, we are ready to define a `Turing` model to perform\n",
    "Bayesian inference on the parameters of the model."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "Turing.@model function logistic_model(\n",
    "    logx, logy, prior_params::NamedTuple=NamedTuple()\n",
    ")\n",
    "    # Define default prior parameters\n",
    "    default_params = (\n",
    "        logic50=(0, 1),\n",
    "        a=(0, 1),\n",
    "        b=(0, 1),\n",
    "        c=(0, 1),\n",
    "        σ²=(-2, 1)\n",
    "    )\n",
    "\n",
    "    # Merge default parameters with provided parameters\n",
    "    params = merge(default_params, prior_params)\n",
    "\n",
    "    # Define priors\n",
    "    logic50 ~ Turing.Normal(params.logic50...)\n",
    "    a ~ Turing.LogNormal(params.a...)\n",
    "    b ~ Turing.LogNormal(params.b...)\n",
    "    c ~ Turing.truncated(Turing.Normal(params.c...), 0, Inf)\n",
    "    σ² ~ Turing.MvLogNormal(\n",
    "        fill(params.σ²[1], size(logx, 1)),\n",
    "        LinearAlgebra.Diagonal(fill(params.σ²[2], size(logx, 1)))\n",
    "    )\n",
    "\n",
    "    # Loop through each row\n",
    "    for i in 1:size(logx, 1)\n",
    "        # Define likelihood\n",
    "        logy[i, :] ~ Turing.MvNormal(\n",
    "            log.(logistic(logx[i, :], a, b, c, logic50)),\n",
    "            fill(σ²[i], size(logx, 2))\n",
    "        )\n",
    "    end # for\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Having defined the model, let's perform inference on the data. First, we need to\n",
    "extract the data into the format required by the model."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 66,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Group data by antibiotic, environment, and day\n",
    "df_group = DF.groupby(\n",
    "    df[(.!df.blank).&(df.concentration_ugmL.>0), :],\n",
    "    [:antibiotic, :env, :day]\n",
    ")\n",
    "\n",
    "# Extract data\n",
    "data = df_group[2]\n",
    "\n",
    "# Get unique concentrations and sort them\n",
    "unique_conc = sort(unique(data.concentration_ugmL))\n",
    "\n",
    "# Count occurrences of each concentration\n",
    "conc_counts = [count(==(c), data.concentration_ugmL) for c in unique_conc]\n",
    "\n",
    "# Create concentration matrix\n",
    "conc_matrix = reduce(\n",
    "    vcat,\n",
    "    [fill(c, count)' for (c, count) in zip(unique_conc, conc_counts)]\n",
    ")\n",
    "\n",
    "# Create OD matrix\n",
    "od_matrix = zeros(length(unique_conc), maximum(conc_counts))\n",
    "\n",
    "# Loop through each concentration\n",
    "for (i, conc) in enumerate(unique_conc)\n",
    "    # Get OD values for the current concentrationm\n",
    "    od_values = data.OD[data.concentration_ugmL.==conc]\n",
    "    # Store OD values in the matrix\n",
    "    od_matrix[i, 1:length(od_values)] = od_values\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Now we can perform inference on the data."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "Random.seed!(42)\n",
    "# Perform inference\n",
    "model = logistic_model(\n",
    "    log.(conc_matrix),\n",
    "    log.(od_matrix),\n",
    ")\n",
    "\n",
    "# Define number of steps\n",
    "n_burnin = 10_000\n",
    "n_samples = 1_000\n",
    "\n",
    "chain = Turing.sample(\n",
    "    model, Turing.NUTS(), Turing.MCMCThreads(), n_burnin + n_samples, 4\n",
    ")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Let's now look at the posterior predictive check. For this, we first generate\n",
    "samples from the posterior predictive distribution."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "Random.seed!(42)\n",
    "\n",
    "# Initialize matrix to store samples\n",
    "logy_samples = Array{Float64}(\n",
    "    undef, size(conc_matrix)..., n_samples\n",
    ")\n",
    "\n",
    "# Convert chain to DataFrame\n",
    "df_chain = DF.DataFrame(chain[n_burnin+1:end, :, :])\n",
    "# Extract index for variances\n",
    "σ²_idx = occursin.([\"σ²\"], names(df_chain))\n",
    "\n",
    "# Loop through samples\n",
    "for i in 1:n_samples\n",
    "    # Generate mean prediction\n",
    "    logμ = log.(\n",
    "        logistic(\n",
    "            log.(conc_matrix),\n",
    "            df_chain[i, :a],\n",
    "            df_chain[i, :b],\n",
    "            df_chain[i, :c],\n",
    "            df_chain[i, :logic50]\n",
    "        )\n",
    "    )\n",
    "    # Extract vector of variances\n",
    "    σ² = collect(df_chain[i, σ²_idx])\n",
    "    # Combine mean prediction and noise\n",
    "    logy_samples[:, :, i] = logμ .+ randn(size(logμ)) .* sqrt.(σ²)\n",
    "end # for\n",
    "\n",
    "# Compute quantiles for 95% and 68% intervals as well as the median\n",
    "logy_quant = reduce(\n",
    "    hcat,\n",
    "    StatsBase.quantile.(\n",
    "        vec.(eachslice(logy_samples, dims=1)),\n",
    "        Ref([0.025, 0.975, 0.18, 0.82, 0.5])\n",
    "    )\n",
    ")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "oNow we can plot the posterior predictive check. as shaded regions."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Initialize figure\n",
    "fig = Figure(size=(350, 300))\n",
    "# Add axis\n",
    "ax = Axis(\n",
    "    fig[1, 1],\n",
    "    xlabel=\"antibiotic concentration\",\n",
    "    ylabel=\"optical density\",\n",
    "    title=\"Posterior Predictive Check\",\n",
    "    xscale=log10,\n",
    "    yscale=log10\n",
    ")\n",
    "\n",
    "# Plot 95% and 68% intervals\n",
    "band!(\n",
    "    ax,\n",
    "    conc_matrix[:, 1],\n",
    "    exp.(logy_quant[1, :]),\n",
    "    exp.(logy_quant[2, :]),\n",
    "    color=(ColorSchemes.Blues_9[5], 1)\n",
    ")\n",
    "band!(\n",
    "    ax,\n",
    "    conc_matrix[:, 1],\n",
    "    exp.(logy_quant[3, :]),\n",
    "    exp.(logy_quant[4, :]),\n",
    "    color=(ColorSchemes.Blues_9[6], 1)\n",
    ")\n",
    "# Plot median\n",
    "lines!(\n",
    "    ax,\n",
    "    conc_matrix[:, 1],\n",
    "    exp.(logy_quant[5, :]),\n",
    "    color=ColorSchemes.Blues_9[7]\n",
    ")\n",
    "\n",
    "# Plot data\n",
    "scatter!(ax, data.concentration_ugmL, data.OD, color=:black)\n",
    "\n",
    "fig"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.5",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
